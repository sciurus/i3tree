#!/usr/bin/env perl

use open qw(:std :utf8);

use strict;
use warnings;
use v5.14;
use AnyEvent::I3;


# receive description of the tree
my $tree = i3->get_tree->recv;


walk_tree( $tree, 0 );

sub display_node {
    my ( $description, $depth, $is_focused ) = @_;
    my $margin = "\t" x $depth;
    my $marker = $is_focused ? "* " : "  ";
    print $marker . $margin . $description . "\n";
    return $depth + 1;
}


sub walk_tree {
    my ( $node, $depth ) = @_;

    my $type        = $node->{'type'};
    my $orientation = $node->{'orientation'};
    my $layout      = $node->{'layout'};
    my $name        = $node->{'name'};
    my $is_focused  = $node->{'focused'};

	# get type and name, for debugging
	# say "$type, $name";

    # if "type" is workspace
    if ( $type eq "workspace" ) {
        $depth = display_node( "Workspace $name ($layout - $orientation)", $depth, $is_focused );
    }

    # if "type" is "con" (type for several kinds of containers)
    if ( $type eq "con" ) {
        # no orientation, so probably a window
        if ( $orientation eq "none" ) {
            # filter out two possible non-windows
            unless ( $name eq "content" or $name =~ "^i3bar" ) {
                $depth = display_node( "Window $name", $depth, $is_focused );
            }
        }
        # orientation, so definitely a split container
        else {
            $depth = display_node( "Split ($layout - $orientation)", $depth, $is_focused );
        }
    }

    foreach ( @{ $node->{'nodes'} } ) {
        walk_tree( $_, $depth );
    }
}

